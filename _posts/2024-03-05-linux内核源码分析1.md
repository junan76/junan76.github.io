---
layout: post
title: 从第一行汇编代码开始
categories: linux内核源码
tags: linux 源码 ARM64 汇编
---

选取linux-3.18.140版本的代码, 分析了ARM64架构上, `start_kernel`函数被调用之前的汇编代码.

为什么要关注启动阶段的汇编代码呢? 对我来说, 只是有点好奇而已. 想知道`start_kernel`之前, 内核都做了什么. 而且, 目前在学习ARM架构, 研究这部分代码, 也可以检验一下对ARM的理解程度.

我的[仓库](https://github.com/junan76/linux-3.18.140)中包含本文使用到的完整代码, 并且包含一些中文注释. 之后会使用这个仓库记录自己的源码阅读过程, 所以代码中会包含更多的注释. 除代码之外, 仓库中还有在qemu上运行调试linux需要使用的根文件系统, qemu的启动脚本. 对于想学习内核的人, 使用起来很方便.

## 0x01 第一行代码的地址是什么

启动阶段的汇编代码在`arch/arm64/kernel/head.S`中, 在这个文件里定义了一个符号`stext`, 部分代码内容如下:

```armasm
ENTRY(stext)
	mov	x21, x0				// x21=FDT
	bl	el2_setup			// Drop to EL1, w20=cpu_boot_mode
	bl	__calc_phys_offset		// x24=PHYS_OFFSET, x28=PHYS_OFFSET-PAGE_OFFSET
	bl	set_cpu_boot_mode_flag
...
ENDPROC(stext)
```

第一条指令就是从这里开始执行的. 我们的目标就是, 弄清楚从这里开始, 如何调用到`start_kernel`函数. 在对`head.S`文件进行搜索之后发现, 在`__mmap_switched`中包含一条指令`b start_kernel`, 但是代码中并没有直接对`__mmap_switched`的调用.

我们可以使用gdb调试器, 跟踪一下代码执行过程. 但当尝试在`stext`上打断点时会发现, 这个断点没起作用. 这是因为, 在执行这段汇编指令时, 处理器的MMU还没有开启, 指令执行过程中使用的还是物理地址, 需要使用物理地址设置断点才能生效. 用`b stext`打断点, 使用的是`stext`这个符号的虚拟地址, 所以断点是没法命中的.

现在的问题是, 怎样获取到第一行指令`mov x21, x0`的物理地址? 或者说它在内存中的什么位置? ARM处理器使用`pc`寄存器记录指令的地址, 只要知道执行指令时`pc`的内容, 也就知道物理地址了.

我们对代码进行修改, 在`mov x21, x0`之后, 增加一个死循环, 修改后的代码如下:

```armasm
  mov	x21, x0				// x21=FDT
1:
  b 1b  // 死循环
  ...
```

重新编译内核, gdb连接成功之后, 执行`continue`命令, 因为死循环的存在, 处理器没有办法执行之后的代码. 在gdb中按下`Ctrl C`, 被调试的linux收到信号, 会停在当前执行的指令上, 也就是死循环的指令, 如下图所示:

![第一条指令的地址](<../assets/img/posts/2024-03-05-linux内核源码分析1/1.png>)

打印出此时`pc`的值为`0x40080164`, 在ARM64架构中, 指令的长度是4个字节, 所以第一条指令`mov x21, x0`的地址就是`0x40080160`, 记下这个地址, 删掉死循环, 重新编译之后就在这个地址上设置断点.

## 0x02 `start_kernel`之前发生了什么

在确定了第一行指令的地址之后, 使用gdb调试, 可以跟踪到`start_kernel`之前的完整逻辑, 具体包括:

- 记录设备树地址

- 计算物理地址和虚拟地址的偏移

- 查找`cpu_table`

- 验证设备树

- 创建初始页表
  
  在开启MMU之前, 必须创建好页表.

- 调用`cpu_table`中的`cpu_setup`函数

- 开启MMU

- 调用`__mmap_switched`, 最终调用到`start_kernel`
  
  开始要执行C语言实现的函数了, C的函数调用依赖于栈, 所以这里必须设置好之后使用的内核栈.

对照下面的注释, 说明上面的步骤:

```armasm
ENTRY(stext)
	/* 
	 * 记录设备树在内存中的地址到x21
	 */
	mov	x21, x0				// x21=FDT
	bl	el2_setup			// Drop to EL1, w20=cpu_boot_mode

	/*
	 * 计算物理地址和虚拟地址的偏移, 保存到x28
	 */
	bl	__calc_phys_offset		// x24=PHYS_OFFSET, x28=PHYS_OFFSET-PAGE_OFFSET
	bl	set_cpu_boot_mode_flag
	mrs	x22, midr_el1			// x22=cpuid
	mov	x0, x22

	/*
	 * 查找cpu_table数组的地址, 并保存到x23
	 */
	bl	lookup_processor_type
	mov	x23, x0				// x23=current cpu_table
	cbz	x23, __error_p			// invalid processor (x23=0)?

	/*
	 * 验证设备树, 创建初始页表
	 */
	bl	__vet_fdt
	bl	__create_page_tables		// x25=TTBR0, x26=TTBR1
	
	/*
	 * The following calls CPU specific code in a position independent
	 * manner. See arch/arm64/mm/proc.S for details. x23 = base of
	 * cpu_info structure selected by lookup_processor_type above.
	 * On return, the CPU will be ready for the MMU to be turned on and
	 * the TCR will have been set.
	 */

	/*
	 * 加载__mmap_switched的虚拟地址到x27, __mmap_switched最终会调用start_kernel,
	 * __switch_data指向的地址还保存了其他重要数据. 
	 */
	ldr	x27, __switch_data		// address to jump to after
						// MMU has been enabled

	/*
	 * 设置lr寄存器的值为__enable_mmu. 所以, 之后的ret指令将返回到__enable_mmu, 此函数的作用是开启MMU,
	 * 初始页表在此函数调用之前已经创建完毕.
	 */
	adr	lr, __enable_mmu		// return (PIC) address

	/*
	 * 获取cpu_table中的cpu_setup函数指针, 转成物理地址之后调用, 该指针指向arch/arm64/mm/proc.S
	 * 中定义的__cpu_setup.
	 */
	ldr	x12, [x23, #CPU_INFO_SETUP]
	add	x12, x12, x28			// __virt_to_phys
	br	x12				// initialise processor
ENDPROC(stext)
```

在`__switch_data`符号处, 保存了一些重要的数据, 包括:
- `__mmap_switched`符号的虚拟地址
- `bss`段的开始和结束地址
- 处理器id
- 设备树地址
- 内核栈地址

在执行`__cpu_setup`之前, 做了两件事情:

- 使用`ldr x27, __switch_data`, 把`__mmap_switched`的地址加载到`x27`寄存器
- 设置`lr`寄存器的值为`__enable_mmu`

`__cpu_setup`执行完毕之后, `ret`指令会返回到`__enable_mmu`;

`__enable_mmu`使能MMU之后, 有一条`b x27`指令, 这样就跳转到了`__mmap_switched`;

`__mmap_switched`会清理`bss`段, 设置内核栈, 之后就会正式调用`start_kernel`;

以上就是`start_kernel`之前, 内核代码完成的一些必要准备.

## 0x03 参考资料

1. https://www.cs.utexas.edu/~rossbach/cs380p/papers/ulk3.pdf
2. https://www.arm.com/architecture/learn-the-architecture/a-profile